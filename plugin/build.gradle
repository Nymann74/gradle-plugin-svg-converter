final REPOSITORY_URL = 'https://github.com/perj74/gradle-plugin-svg-converter'

final GROUP_ID = 'com.trifork.android.tools'
final ARTIFACT_ID = 'gradle-plugin-svg-converter'
final VERSION = '0.0.1'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.+'
    }
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'nexus'

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'com.android.tools.build:gradle:+'
    compile 'org.apache.xmlgraphics:batik-codec:1.7'
    compile 'org.apache.xmlgraphics:batik-rasterizer:1.7'
    compile 'org.apache.commons:commons-io:1.3.2'
}

// artifact settings

String git(String command) {
    println "git $command"
    def process = ["sh", "-c", "cd ${project.rootDir} ; git $command"].execute()
    if (process.waitFor() != 0) {
        throw new GradleException(process.err.text)
    }
    return process.in.text

}

task('releng') {
    doLast {
        def gitStatus = git("status --porcelain")
        if (gitStatus.trim()) {
            throw new GradleException("Changes found:\n$gitStatus")
        }

        println git("tag v$VERSION")
        println git("push origin v$VERSION")
    }
}

configurations {
    mavenDeployer
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

uploadArchives {
    repositories.mavenDeployer {
        pom.groupId = GROUP_ID
        pom.artifactId = ARTIFACT_ID
        pom.version = VERSION
    }
}

modifyPom {
    project {
        name 'Android Gradle Plugin SVG Converter'
        description 'SVG Converter for android'
        url REPOSITORY_URL
        inceptionYear '2014'

        scm {
            url REPOSITORY_URL
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'perj74'
                name 'Per Nymann JÃ¸rgensen'
                email 'pnj@trifork.com'
            }
        }
    }
}
